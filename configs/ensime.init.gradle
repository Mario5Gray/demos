apply plugin: AddEnsimePlugin

class AddEnsimePlugin  implements Plugin<Gradle> {
  def supportedPlugins = [
    'org.gradle.api.plugins.JavaPlugin',
    'org.gradle.api.plugins.ScalaPlugin',
    'jp.leafytree.gradle.AndroidScalaPlugin'
  ]

  void apply(Gradle gradle) {
    def added = false

    gradle.allprojects { project ->
      project.with {
        if (parent == null) {

          buildscript { 
	    repositories {
	      mavenLocal()
	      mavenCentral()

              jcenter()
            }
	    //if using snapshot, change rootProject.ensime.serverVersion to a snapshot
	    //update this property to the lated ensime-gradle version
            dependencies {
	      classpath 'net.coacoas.gradle:ensime-gradle:0.3.0-SNAPSHOT'
	    }
          }
        }

        plugins.whenPluginAdded { plugin ->
          if (!added && supportedPlugins.contains(plugin.class.name)) { 
            rootProject.apply plugin: 'org.ensime.gradle'
	    //update the server version to the latest supported by the ensime-gradle plugin
	    rootProject.ensime.serverVersion = "2.0.0-SNAPSHOT"
            added = true
          }
        }
      }
    }
  }
}
